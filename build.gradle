import java.text.SimpleDateFormat

plugins {
    id 'application'
}

group 'com.reportmill'
version new SimpleDateFormat("yyyy.MM").format(new Date())

sourceSets.main.java.srcDirs = ['src']
sourceSets.main.resources.srcDirs = ['src']

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
}

dependencies {

    // SnapKit project
    implementation project(':SnapKit')

    // SnapKitGL project
    implementation project(':SnapKitGL')

    // SnapBuilder project
    implementation project(':SnapBuilder')

    // Tools jar
    implementation files('lib/tools.jar')

    // JGit
    implementation 'org.eclipse.jgit:org.eclipse.jgit:5.13.3.202401111512-r'

    // So we can get jars
    testImplementation project(':CJDom')
    testImplementation project(':SnapCJ')
}

mainClassName = 'snapcode.app.App'

/**
 * Custom task to build jar that includes all dependencies.
 */
task buildFullJar(type: Jar) {
    archiveBaseName = 'SnapCodeAll'
    manifest {
        attributes(
                'Main-Class': 'snapcode.app.App',
                'Application-Name': 'SnapCode',
        )
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    exclude('**/org/**', '**/test/**') //, '**/sun/**')
    with jar
    dependsOn build
}

task copyCJ(type: Copy) {
    from (
            "../CJDom/cjdom.js",
            "../CJDom/build/libs/CJDom-" + new SimpleDateFormat("yyyy.MM").format(new Date()) + ".jar",
            "../SnapCJ/build/libs/SnapCJ-" + new SimpleDateFormat("yyyy.MM").format(new Date()) + ".jar",
            "../SnapKit/build/libs/SnapKit-" + new SimpleDateFormat("yyyy.MM").format(new Date()) + ".jar",
            "../SnapBuilder/build/libs/SnapBuilder-" + new SimpleDateFormat("yyyy.MM").format(new Date()) + ".jar",
            "../SnapCharts/build/libs/SnapCharts-" + new SimpleDateFormat("yyyy.MM").format(new Date()) + ".jar"
    )
    into "build/libs"
    dependsOn buildFullJar
}
