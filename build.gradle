import java.text.SimpleDateFormat

plugins {
    id 'application'
}

group 'com.reportmill'
version new SimpleDateFormat("yyyy.MM").format(new Date())

sourceSets.main.java.srcDirs = ['src']
sourceSets.main.resources.srcDirs = ['src']

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
}

dependencies {

    // SnapKit project
    implementation project(':SnapKit')

    // SnapCharts project
    implementation project(':SnapCharts')

    // SnapKitGL project
    implementation project(':SnapKitGL')

    // SnapBuilder project
    implementation project(':SnapBuilder')

    // Tools jar
    implementation files('lib/tools.jar')
}

mainClassName = 'snapcode.app.App'

/**
 * Custom task to build jar that includes all dependencies.
 */
task buildFullJar(type: Jar) {
    archiveBaseName = 'SnapCodeAll'
    manifest {
        attributes(
                'Main-Class': 'snapcode.app.App',
                'Application-Name': 'SnapCode',
        )
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    exclude('**/org/**', '**/test/**', '**/font_metrics.properties')
    with jar
    dependsOn build
}

